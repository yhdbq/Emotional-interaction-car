# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
} else {
  import("//build/ohos.gni")
  import("//foundation/appexecfwk/standard/appexecfwk.gni")
}

################################################################################

local_base_sources = [
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/dhcp_manage/mgr_service/src/dhcp_client_service_impl.cpp",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/dhcp_manage/mgr_service/src/dhcp_event_subscriber.cpp",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/dhcp_manage/mgr_service/src/dhcp_func.cpp",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/dhcp_manage/mgr_service/src/dhcp_server_service.cpp",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/dhcp_manage/mgr_service/src/dhcp_service.cpp",
]

local_base_include_dirs = [
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/common/log",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/dhcp_manage/mgr_service/include",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/dhcp_manage/mgr_service/interfaces",
]

if (defined(ohos_lite)) {
  shared_library("dhcp_manager_service") {
    sources = local_base_sources

    include_dirs = local_base_include_dirs
    include_dirs += [
      "//base/hiviewdfx/hilog_lite/interfaces/native/innerkits",
      "//third_party/bounds_checking_function/include",
    ]

    deps = [
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//third_party/bounds_checking_function:libsec_shared",
    ]

    defines = [
      "_GNU_SOURCE",
      "OHOS_ARCH_LITE",
    ]
    configs -= [ "//build/lite/config:language_cpp" ]
    cflags_cc = [
      "-fPIC",
      "-std=c++17",
      "-Wall",
    ]
    ldflags = [
      "-fPIC",
      "-Wl,-E",
    ]
  }
} else {
  ohos_shared_library("dhcp_manager_service") {
    install_enable = true
    sources = local_base_sources

    include_dirs = local_base_include_dirs
    include_dirs += [
      "//utils/native/base/include",
      "//foundation/appexecfwk/standard/interfaces/innerkits/libeventhandler/include",
      "//foundation/communication/ipc/interfaces/innerkits/ipc_core/include",
    ]

    deps = [
      "${aafwk_path}/interfaces/innerkits/base:base",
      "${aafwk_path}/interfaces/innerkits/want:want",
      "//utils/native/base:utils",
    ]

    cflags_cc = [
      "-std=c++17",
      "-Wall",
    ]

    external_deps = [
      "ces_standard:cesfwk_innerkits",
      "hiviewdfx_hilog_native:libhilog",
    ]
    ldflags = [
      "-fPIC",
      "-Wl,-E",
    ]

    part_name = "wifi_standard"
    subsystem_name = "communication"
  }
}
