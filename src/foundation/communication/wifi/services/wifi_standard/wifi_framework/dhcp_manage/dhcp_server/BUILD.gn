# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
} else {
  import("//build/ohos.gni")
}

local_base_sources = [
  "src/address_utils.c",
  "src/common_util.c",
  "src/dhcp_address_pool.c",
  "src/dhcp_argument.c",
  "src/dhcp_binding.c",
  "src/dhcp_config.c",
  "src/dhcp_dhcpd.c",
  "src/dhcp_option.c",
  "src/dhcp_server.c",
  "src/hash_table.c",
]

local_base_include_dirs = [
  "include",
  "include/common",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/dhcp_manage/dhcp_client/include",
]

dhcp_hilog_enable = true

if (defined(ohos_lite)) {
  executable("dhcp_server") {
    sources = local_base_sources

    include_dirs = local_base_include_dirs
    include_dirs += [
      "//base/hiviewdfx/hilog_lite/interfaces/native/innerkits",
      "//third_party/bounds_checking_function/include",
    ]

    deps = [ "//third_party/bounds_checking_function:libsec_shared" ]

    cflags_cc = [ "-fno-rtti" ]

    defines = [
      "__OHOS__",
      "_GNU_SOURCE",
      "OHOS_ARCH_LITE",
    ]
    if (dhcp_hilog_enable) {
      deps += [ "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared" ]
      defines += [ "DHCP_HILOG_ENABLE" ]
    }
  }
} else {
  ohos_executable("dhcp_server") {
    install_enable = true
    sources = local_base_sources

    include_dirs = local_base_include_dirs
    include_dirs += [ "//utils/native/base/include" ]

    deps = [ "//utils/native/base:utils" ]

    cflags_cc = [ "-fno-rtti" ]

    defines = [ "__OHOS__" ]
    if (dhcp_hilog_enable) {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
      defines += [ "DHCP_HILOG_ENABLE" ]
    }

    part_name = "wifi_standard"
    subsystem_name = "communication"
  }
}
