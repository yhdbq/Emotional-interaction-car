# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//base/hiviewdfx/hiview/hiview.gni")
import("//build/ohos.gni")

config("hiview_core_config") {
  visibility = [ "*:*" ]
  include_dirs = [
    ".",
    "$hiview_root/include",
    "$hiview_base/utility/include",
    "$hiview_adapter/utility/include",
  ]
  cflags_cc = [ "-D__HIVIEW_OHOS__" ]

  if (use_musl) {
    cflags_cc += [ "-DUSE_MUSL" ]
  }
}

ohos_source_set("hiview_core") {
  public_configs = [ ":hiview_core_config" ]

  sources = [
    "hiview_platform.cpp",
    "plugin_config.cpp",
  ]

  deps = [
    "$hiview_adapter/utility:hiview_adapter_utility",
    "$hiview_base:hiviewbase",
  ]
}

ohos_source_set("sysevent_source") {
  public_configs = [ ":hiview_core_config" ]

  sources = [
    "event_server.cpp",
    "sysevent_source.cpp",
  ]

  deps = [
    "$hiview_adapter/system_service:system_service",
    "$hiview_base:hiviewbase",
  ]

  if (use_musl) {
    include_dirs = [ "//base/startup/init_lite/interfaces/innerkits/include" ]

    deps += [ "//base/startup/init_lite/interfaces/innerkits/socket:libsocket" ]
  }

  if (build_public_version) {
    deps += [ "//utils/native/base:utilsecurec_shared" ]
  } else {
    deps += [ "//utils/native/base:utilsecurec" ]
  }
}

group("unittest") {
  testonly = true
  deps = []
}

group("moduletest") {
  testonly = true
  deps = []
}
